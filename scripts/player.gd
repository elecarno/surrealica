class_name Player
extends CharacterBody3D


var active: bool = false

const SPEED = 5.0
const JUMP_VELOCITY = 4.5
const SENSITIVITY = 50

var look_direction: Vector2 = Vector2.ZERO

@onready var cam: Camera3D = $cam

func _enter_tree() -> void:
	set_multiplayer_authority(name.to_int())

func toggle_control(state: bool):
	active = state
	cam.current = state
	$mesh.visible = state
	if state:
		Input.mouse_mode = Input.MOUSE_MODE_CAPTURED
	else:
		Input.mouse_mode = Input.MOUSE_MODE_VISIBLE

func _physics_process(delta: float) -> void:
	if not is_multiplayer_authority(): return
	if not active: return

	if Input.is_action_just_pressed("escape"):
		Input.mouse_mode = Input.MOUSE_MODE_VISIBLE
	if Input.is_action_just_pressed("lmb"):
		Input.mouse_mode = Input.MOUSE_MODE_CAPTURED
	
	# Add the gravity
	if not is_on_floor():
		velocity += get_gravity() * delta

	# Handle jump
	if Input.is_action_just_pressed("jump") and is_on_floor():
		velocity.y = JUMP_VELOCITY

	# Get the input direction and handle the movement/deceleration
	# As good practice, you should replace UI actions with custom gameplay actions
	var input_dir := Input.get_vector("left", "right", "forward", "backward")
	var direction := (transform.basis * Vector3(input_dir.x, 0, input_dir.y)).normalized()
	if direction:
		velocity.x = direction.x * SPEED
		velocity.z = direction.z * SPEED
	else:
		velocity.x = move_toward(velocity.x, 0, SPEED)
		velocity.z = move_toward(velocity.z, 0, SPEED)

	_rotate_cam(delta)
	move_and_slide()
	
func _input(event: InputEvent) -> void:
	if event is InputEventMouseMotion: look_direction = event.relative * 0.01
	
func _rotate_cam(delta: float, sensitivity_mod: float = 1.0):
	rotation.y -= look_direction.x * SENSITIVITY * delta
	cam.rotation.x = clamp(
		cam.rotation.x - look_direction.y * SENSITIVITY * sensitivity_mod * delta, -1.5, 1.5
		)
	look_direction = Vector2.ZERO
